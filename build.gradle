import org.gradle.internal.jvm.Jvm

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        // If we upload to 3.1.4 then you will be asked to set builtools in 27.0.3
        // If we upload buildtools to 27.0.3 we will have to upload appcompat to 27.0.3 and
        // we can't do so because release won't work on MercadoLibre's App.
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.monits:static-code-analysis-plugin:2.6.7'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    }
}

ext {
    travisBuild = System.getenv("TRAVIS") as boolean
    dateNow = project.hasProperty('deploy') ? "" : new Date().format("ddMMyyyyHHmmssS")
}

subprojects {
    repositories {
        mavenLocal()
        jcenter()
        google()
        maven {
            url "http://dl.bintray.com/mercadolibre/android-public"
        }
    }

}

wrapper {
    gradleVersion = '4.9'
    distributionType = Wrapper.DistributionType.BIN
}

// Aggregate javadoc task
rootProject.gradle.projectsEvaluated {
    task javadoc(type:Javadoc) { Javadoc self ->
        failOnError = false
        options.encoding = 'UTF-8'

        // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
        if (Jvm.current().getJavaVersion().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }

        destinationDir = file("$buildDir/docs/javadoc")

        subprojects.each {
            it.tasks.withType(Javadoc).all { task ->
                self.dependsOn += task.dependsOn
                self.source += task.source
                self.classpath += task.classpath
                self.excludes += task.excludes
            }
        }
    }
}

