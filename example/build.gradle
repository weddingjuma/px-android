apply plugin:'com.android.application'

android {
    compileSdkVersion api_level as int
    buildToolsVersion build_tools_version

    testBuildType "release"

    defaultConfig {
        applicationId "com.mercadopago.example"
        minSdkVersion min_api_level as int
        targetSdkVersion api_level as int
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // only on development
        if (!project.hasProperty('production')) {
            resConfigs("en", "xxhdpi")
            aaptOptions.cruncherEnabled = false
        }
    }

    dexOptions {
        preDexLibraries !(travisBuild)
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {

        debug {
            versionNameSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            debuggable true
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug_keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
}


dependencies {
    implementation project(':px-checkout')
    implementation "com.google.code.gson:gson:$gson"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"

    implementation "com.android.support.constraint:constraint-layout:$constraint_layout"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_cannary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_cannary"
    androidTestImplementation project(':px-testlib')
    androidTestImplementation project(':testlib')
}