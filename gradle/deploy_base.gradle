import org.gradle.internal.jvm.Jvm

apply plugin:'maven'
apply plugin:'signing'
apply plugin:'maven-publish'
apply plugin:'com.jfrog.bintray'
apply plugin:'com.github.dcendents.android-maven'

def versionToDeploy = version_to_deploy as String
versionToDeploy = project.hasProperty('deploy') ? versionToDeploy : 'EXPERIMENTAL-' + versionToDeploy + '-' + dateNow 

version = versionToDeploy

task javadoc(type:Javadoc) {
    failOnError = false
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict than 1.7. This makes JDK 1.8 behave like 1.7 for javadoc
    if (Jvm.current().getJavaVersion().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'
}

task androidJavadocsJar(type:Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task androidSourcesJar(type:Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task deployTobintray {
    finalizedBy 'bintrayUpload'

    doLast {
        println 'Deploying ' + project.name + ' version: ' + versionToDeploy
    }
}

afterEvaluate {
    javadoc.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

def localProperties = new Properties()

if (project.rootProject.file('local.properties').exists()) {
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
}

install {
    group = groupName
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId group
                artifactId artifactLib

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id 'valeria.serber'
                        name 'Valeria Serber'
                        email 'valeria.serber@mercadolibre.com'
                    }
                    developer {
                        id 'angeles.arlanti'
                        name 'Maria De Los Angeles Arlanti'
                        email 'angeles.arlanti@mercadolibre.com'
                    }
                    developer {
                        id 'matias.romar'
                        name 'Matias Romar'
                        email 'matias.romar@mercadolibre.com'
                    }
                    developer {
                        id 'lucas.bais'
                        name 'Lucas Bais'
                        email 'lucas.bais@mercadolibre.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


bintray {
    version = versionToDeploy
    user = localProperties.getProperty("bintray.user", "")
    key = localProperties.getProperty("bintray.apikey", "")

    override = true

    configurations = ['archives']
    pkg {
        repo = project.hasProperty('deploy') ? 'android' : 'android-experimental' 
        name = libraryName
        desc = libraryDescription
        userOrg = 'mercadopago'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            name = versionToDeploy
            desc = libraryDescription
            vcsTag = versionToDeploy
            released = project.hasProperty('deploy') ? new Date() : null
        }
    }
}